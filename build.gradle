apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

def sonatypeUsername = hasProperty('sonatypeUsername') ? sonatypeUsername : 'username'
def sonatypePassword = hasProperty('sonatypePassword') ? sonatypePassword : 'password'

group = 'com.zachary-moore'
version = '1.1.0'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

publishing {
    publications {
        mavenJava(MavenPublication) {
            pom {
                name = 'JSONCustomLintr'
                description = 'Library for the creation, running, and reporting of Custom Lint Rules for files that follow JSON Notation.'
                url = 'https://github.com/zsmoore/JSONCustomLintr'

                organization {
                    name = 'com.zachary-moore'
                    url = 'https://www.zachary-moore.com/'
                }

                issueManagement {
                    system = 'GitHub'
                    url = 'https://github.com/zsmoore/JSONCustomLintr/issues'
                }

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://github.com/zsmoore/JSONCustomLintr/blob/master/LICENSE'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'zsmoore'
                        name = 'Zachary moore'
                        email = 'zsmoore@zachary-moore.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/zsmoore/JSONCustomLintr.git'
                    developerConnection = 'scm:git:ssh://git@github.com:zsmoore/JSONCustomLintr.git'
                    url = 'https://github.com/zsmoore/JSONCustomLintr'
                }
            }

            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }

    repositories {
        maven {
            url 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
            credentials {
                username = sonatypeUsername
                password = sonatypePassword
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

tasks.withType(Sign) {
    onlyIf { isReleaseVersion }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation group: 'org.json', name: 'json', version: '20180813'
    implementation group: 'com.j2html', name: 'j2html', version: '1.4.0'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.4.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.27.0'

    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.4.2'
}
